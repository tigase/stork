apply plugin: 'com.android.application'

configurations {
	playstoreImplementation
	freeImplementation
}

android {
	compileSdkVersion 29
//	buildToolsVersion "27.0.3"

	defaultConfig {
		applicationId "org.tigase.messenger.phone.pro"
		minSdkVersion 29
		targetSdkVersion 29
		versionCode 92
		versionName "3.2.1"
		multiDexEnabled true

//		jackOptions {
//			enabled true
//		}
	}
	signingConfigs {
		release {
			storeFile file(System.getProperty("user.home") + "/android-release.keystore")
			keyAlias ''
			storePassword ''
			keyPassword ''
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
		debug {
			debuggable true
			versionNameSuffix " Debug"
		}
	}
	compileOptions {
//		sourceCompatibility JavaVersion.VERSION_1_8
//		targetCompatibility JavaVersion.VERSION_1_8
	}
	lintOptions {
		abortOnError false
	}

	flavorDimensions("distribution")

	productFlavors {
		free {
			dimension "distribution"
			versionNameSuffix ".f"
		}
		playstore {
			dimension "distribution"
			versionNameSuffix ".p"
		}
	}

	sourceSets {
		free {
			java {
				srcDirs 'src/free/java'
			}
		}
		playstore {
			java {
				srcDirs 'src/playstore/java'
			}
		}
	}

}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	testImplementation 'junit:junit:4.13.2'
	implementation 'tigase:jaxmpp-android:3.4.0'
	implementation 'tigase:jaxmpp-muc:3.4.0'
	implementation 'tigase:jaxmpp-chat:3.4.0'
	implementation 'tigase:jaxmpp-deliveryreceipts:3.4.0'
	implementation 'tigase:jaxmpp-presence:3.4.0'
	implementation 'tigase:jaxmpp-roster:3.4.0'
	implementation 'tigase:jaxmpp-registration:3.4.0'
	implementation 'tigase:jaxmpp-vcard:3.4.0'
	implementation 'tigase:jaxmpp-filetransfer:3.4.0'
	implementation 'tigase:jaxmpp-socks5:3.4.0'
	implementation 'tigase:jaxmpp-pubsub:3.4.0'
	implementation 'tigase:jaxmpp-im:3.4.0'
	implementation 'tigase:jaxmpp-core:3.4.0'
	implementation 'tigase:jaxmpp-adhoc:3.4.0'
	implementation 'tigase:jaxmpp-push:3.4.0'
	implementation 'tigase:jaxmpp-xep0136:3.4.0'
	implementation 'tigase:jaxmpp-mam:3.4.0'
	implementation 'tigase:jaxmpp-httpfileupload:3.4.0'
	implementation 'tigase:jaxmpp-omemo:3.4.0'
	implementation 'me.leolin:ShortcutBadger:1.1.22@aar'
	implementation 'org.webrtc:google-webrtc:1.0.32006'
	playstoreImplementation('com.google.firebase:firebase-messaging:22.0.0')
	implementation 'org.jetbrains:annotations:15.0'

	implementation 'com.android.support:multidex:2.0.1'
	implementation 'androidx.core:core-ktx:1.6.0'
	implementation 'androidx.appcompat:appcompat:1.3.1'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation "androidx.preference:preference:1.1.1"
	implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
	implementation 'com.google.android.material:material:1.4.0'
}

afterEvaluate {project ->
	def propsFile = rootProject.file('keystore.properties')
	def configName = 'release'

	if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
		def props = new Properties()
		props.load(new FileInputStream(propsFile))
		android.signingConfigs[configName].storeFile = file(props['storeFile'])
		android.signingConfigs[configName].storePassword = props['storePassword']
		android.signingConfigs[configName].keyAlias = props['keyAlias']
		android.signingConfigs[configName].keyPassword = props['keyPassword']
	}
}

apply plugin: 'com.google.gms.google-services'
